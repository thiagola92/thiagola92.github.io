---
authors: thiagola92
tags: [godot, gui, tips]
---

# Godot GUI Tips

## 1 - Performance
Mude o modo de renderiza√ß√£o para *Compatibility*, n√£o √© como se voc√™ fosse precisar de qualquer renderiza√ß√£o al√©m do b√°sico para desenhar uma janela na tela.  

![Compatibility](compatibility.png)  

Ative *Application > Run > Low Processor Mode* para que tenha um delay entre as renderiza√ß√µes da janela e apenas renderizar se alguma mudan√ßa for detectada. Em jogos a tela muda constantemente, ent√£o esse tipo de delay e valida√ß√£o s√≥ atrapalham mas como estamos falando de GUI que altera bem menos, isto ajuda muito.  

![Low Processor Mode](low_processor_mode.png)  

## 2 - Window Title Bar
Por padr√£o o nosso sistema operacional nos provid√™ncia o gr√°fico b√°sico de uma janela e nos deixa respons√°vel por desenhar o conte√∫do dentro dela.  

![Not borderless](not_borderless.png)  

O lado positivo √© que isto nos provid√™ncia o b√°sico de uma janela, como aqueles 3 bot√µes no topo da direita (minimizar, m√°ximizar, fechar, ...).  

Por√©m note que, dependendo do sistema operacional, mais op√ß√µes podem estar dispon√≠veis! Se eu clicar com o bot√£o direito na title bar do topo, podemos ver mais a√ß√µes:  

![Not borderless again](not_borderless_again.png)  

Se ativarmos *Display > Window > Size > Borderless*, o sistema operacional deixar√° de adicionar a title bar no topo:  

![Borderless](borderless.png)  

Basicamente ele est√° assumindo que voc√™ mesmo ir√° desenhar a title bar no topo caso queira (normalmente em jogos isto n√£o faz sentido).  

:::info
Borderless ou n√£o, ainda se trata de uma janela no seu sistema operacional ent√£o alguns atalhos podem continuar funcionando (`Super + Up/Down/Left/Right`, `Alt + Space`).  
:::

## 3 - Multiple Windows
Janelas abertas s√£o tratadas como processos filhos, ou seja, o encerramento de uma janela pai ir√° encerrar os filhos.  

Caso queiramos ter m√∫ltiplas janelas id√™nticas, igual a editores de textos e navegadores, precisamos ter certeza que a janela principal (processo inicial) n√£o possa ser encerrado da maneira padr√£o (clickando no bot√£o de fechar).  

Podemos resolver isto escondendo a janela principal e apenas exibindo as subwindows.  

Ative *Display > Window > Size > Transparent* para que o fundo cinza padr√£o n√£o seja renderizado durante a execu√ß√£o.  

![Transparent background](transparent.png)  

:::note
Acredito que a cor padr√£o do canvas √© preto, por isto deixar de pintar vai deixar a janela preta
:::

Ative *Display > Window > Per Pixel Transparency > Allowed* para que o fundo realmente seja transparente (caso contr√°rio vai ficar o canvas preto).  

![Per Pixel Transparency](per_pixel_transparency.png)  

:::warning
Existe uma configura√ß√£o que eu ainda n√£o entendi a necessidade: *Rendering > Viewport > Transparent Background*.  

Mas a documenta√ß√£o menciona ela como necess√°ria.  
:::

**Como visto na sess√£o anterior...**  

Ative Display > Window > Size > Borderless para o sistema operacional deixar√° de adicionar a title bar no topo.  

![Invisible](invisible.png)  

Embora ela esteja transparente, ela ainda √© uma janela como as outras. Podemos conferir que ela ainda aparece quando apertando Alt+Tab (ou apenas apertando Alt no Ubuntu).  

![Window invisible but exist](window_invisible.png)  

Agora n√≥s temos que tratar inputs!

**Primeiro** podemos notar que se est√° janela for posta na frente de outra, ela n√£o ir√° deixar de consumir os seus clicks (mesmo que voc√™ queira selecionar algo na janela de tr√°s).  

Para resolver isto podemos alterar janela raiz (criada quando seu programa inicia) para repassar adiante clicks do mouse.  

```gdscript
func _ready() -> void:
	get_window().mouse_passthrough = true
```

**Segundo** podemos notar que ela ainda est√° processando teclas (pode ser selecionada pelo `Alt + Tab`, fechada por `Alt + F4`, maximizada com `Super + Up`, etc).  

Ative *Display > Window > Size > No Focus* para que ela n√£o possa ser focada (at√© por atalhos).  

![Process invisible](process_invisible.png)  

Lembre que fechar o processo pai fecha todos os filhos, por√©m fechar todos os filhos n√£o fecha o pai.

Isto quer dizer que o processo pai continua rodando mesmo se o usu√°rio fechar todas as janelas filhas. Agora o usu√°rio apenas conseguiria encerrar o programa pelo "gerenciador de tarefas" ou terminal.  

Para tratar isto podemos ligar um signal a um m√©todo respons√°vel por notar quando a quantidade de filhos mudar e encerrar o programa se necess√°rio.  

![Signal child_order_changed](child_order_changed.png)

```gdscript
func _on_child_order_changed() -> void:
	if get_child_count() == 0:
		get_tree().quit()
```

:::note
Isto √© apenas uma maneira de tratar!  

N√≥s poderiamos checar a cada frame se todas as janelas foram fechadas, poderiamos fazer os filhos avisarem o pai quando fossem encerrados, etc.  
:::

Agora precisamos entender que cada janela aberta √© uma subwindow. Existem dois tipos de subwindows:
1. Subwindows
	- Quando sua janela pede ao sistema operacional para criar uma janela filha dela
	- Sua janela filha vai possuir a title bar padr√£o de janelas
2. Embed subwindows
	- Quando sua janela simula outra janela dentro dela mesmo
	- Isto impossibilita ela de ser mover para fora da janela pai

Se estamos tratando de uma aplica√ß√£o que possui m√∫ltiplas janelas, precisamos que ela se mova para fora da janela pai. Caso contr√°rio isso ocorreria:  

![Subwindow + Half subwindow](subwindow_one_half.png)  

A janela 2 est√° saindo do limite da janela pai.  

Poderiamos inicializar a janela pai maximizada para evitar isto por√©m outros problemas iriam aparecer, por exemplo: Janela pai ignorar clicks e teclas, tornando imposs√≠vel interagir com as janelas simuladas nele.  

Desative *Display > Window > Subwindows > Embed Subwindows* para que as subwindows sejam tratadas como janelas reais pelo sistema operacional (em vez de simuladas pela janela pai).  

![Subwindows](subwindows.png)  

Mas se quisermos ter uma title bar de janela √∫nica para as nossas janelas? Podemos fazer o mesmo que fizemos com janela principal, torna-la borderless.  

![subwindows borderless](subwindows_borderless.png)  

Dentro das propriedades da Janela, ative *Flags > Borderless*.  

![subwindows borderless activate](subwindows_borderless2.png)  

Agora n√≥s seriamos respons√°veis por criar a title bar no topo da janela. Desta maneira poderiamos fazer uma title bar √∫nica igual ao Google Chrome ou Steam!  

## 4 - Custom Title Bar
Ter uma title bar pr√≥pria √© relativamente raro hoje em dia, pois muitas vezes requer reinventar a roda sem trazer benif√≠cios reais.  

Mas isto n√£o quer dizer que nenhuma aplica√ß√£o faz isto:  
![Custom Title Bars](custom_title_bars.png)  
<sub>(Steam, GNOME Files, Google Chrome)</sub>  

Note que as 3 aplica√ß√µes aproveitaram o espa√ßo para provid√™nciar mais informa√ß√µes e funcionalidades ao usu√°rio. Por√©m n√≥s vamos focar em pelo menos reproduzir o b√°sico:  
1. Exibir titulo
2. Provid√™nciar bot√µes de minimizar, maximizar e fechar
3. Double click maximizar
4. Arrastar a title bar deve mover a janela
5. Redimensionar janela se arrastar as bordas

Depois disso voc√™ deve ser capaz de adicionar ou remover mais utilidades conforme a sua vontade.  

:::warning
Estarei partindo do princ√≠pio que queremos customizar uma title bar na janela principal, por isto o c√≥digo utiliza `get_window()`, mas adapta√ß√µes podem ser necess√°rias caso esteja tratando subwindows.  
:::

### Exibir Titulo
Basta utilizar o node Label.  

### Minimize, Maximize, Close Buttons
Basta utilizar 3 nodes Button tratando o signal `pressed`:  

```gdscript
func _on_minimize_pressed() -> void:
	get_window().mode = Window.MODE_MINIMIZED


func _on_maximize_pressed() -> void:
	if get_window().mode == Window.MODE_MAXIMIZED:
		get_window().mode = Window.MODE_WINDOWED
	else:
		get_window().mode = Window.MODE_MAXIMIZED


func _on_close_pressed() -> void:
	get_tree().quit()
```

:::tip subwindows tip
√â importante tratar o signal `close_requested` vindo da janela, pois √© por ele que voc√™ recebe notifica√ß√µes que o usu√°rio tentou fechar de outras maneiras (taskbar do windows, etc).  
:::

### Double Click Maximize
Container n√£o possui signal para isto diretamente por√©m podemos utilizar o signal mais geral `gui_input`.  

```gdscript
func _on_minimize_pressed() -> void:
	...


func _on_maximize_pressed() -> void:
	...


func _on_close_pressed() -> void:
	...


// highlight-start
func _on_title_bar_gui_input(event: InputEvent) -> void:
	if event is InputEventMouseButton:
		_on_title_bar_mouse_button(event)


func _on_title_bar_mouse_button(event: InputEventMouseButton) -> void:
	if event.button_index == MOUSE_BUTTON_LEFT and event.double_click:
		_on_title_bar_double_click()


func _on_title_bar_double_click() -> void:
	match get_window().mode:
		Window.MODE_MAXIMIZED:
			get_window().mode = Window.MODE_WINDOWED
		_:
			get_window().mode = Window.MODE_MAXIMIZED
// highlight-end
```

J√° estamos dividindo em fun√ß√µes menores pois os passos seguintes ir√£o adicionar mais funcionalidades nestas fun√ß√µes gerais.  

### Drag Window
A princ√≠pio, arrastar a janela pode ser resumido em saber duas coisas:  
- Saber se o click do mouse est√° sendo pressionado
- Onde que o click estava quando come√ßou

```gdscript
// highlight-start
var _title_bar_dragging: bool = false

var _title_bar_dragging_start: Vector2i
// highlight-end


func _on_minimize_pressed() -> void:
	...


func _on_maximize_pressed() -> void:
	...


func _on_close_pressed() -> void:
	...


func _on_title_bar_gui_input(event: InputEvent) -> void:
	if event is InputEventMouseButton:
		_on_title_bar_mouse_button(event)
	// highlight-start
	elif event is InputEventMouseMotion:
		_on_title_bar_mouse_motion(event)
	// highlight-end


func _on_title_bar_mouse_button(event: InputEventMouseButton) -> void:
	if event.button_index == MOUSE_BUTTON_LEFT and event.double_click:
		_on_title_bar_double_click()
	// highlight-start
	elif event.button_index == MOUSE_BUTTON_LEFT and event.pressed:
		_title_bar_dragging = true
		_title_bar_dragging_start = get_global_mouse_position()
	elif event.button_index == MOUSE_BUTTON_LEFT and not event.pressed:
		_title_bar_dragging = false
	// highlight-end


func _on_title_bar_double_click() -> void:
	...


// highlight-start
func _on_title_bar_mouse_motion(_event: InputEventMouseMotion) -> void:
	if _title_bar_dragging:
		_on_title_bar_dragged()


func _on_title_bar_dragged() -> void:
	match get_window().mode:
		Window.MODE_WINDOWED:
			get_window().position += get_global_mouse_position() as Vector2i - _title_bar_dragging_start
// highlight-end
```

:::tip subwindows tip
**Primeiro**: Talvez seja bom mover para o centro da tela a janela pois a posi√ß√£o poder n√£o estar correta durante a inicializa√ß√£o (bug?):  

```gdscript
func _ready() -> void:
	get_window().move_to_center()
```

**Segundo**: Talvez seja necess√°rio utilizar `get_local_mouse_position()` em vez de `get_global_mouse_position()` pois deve ser necess√°rio o canvas da pr√≥pria subwindow.  
:::

Esse foi apenas o essencial sobre arrastar, agora podemos pensar em implementar detalhes sobre a a√ß√£o de arrastar janelas.  

Por exemplo: Quando o usu√°rio tentar arrastar uma janela m√°ximizada, ela automaticamente sai do m√°ximizado e se posiciona para que o mouse esteja proporcionalmente na posi√ß√£o correta.   

```gdscript
var _title_bar_dragging: bool = false

var _title_bar_dragging_start: Vector2i

// highlight-next-line
var _title_bar_dragging_adjustment: float = 0


func _on_minimize_pressed() -> void:
	...


func _on_maximize_pressed() -> void:
	...


func _on_close_pressed() -> void:
	...


func _on_title_bar_gui_input(event: InputEvent) -> void:
	...


func _on_title_bar_mouse_button(event: InputEventMouseButton) -> void:
	...


func _on_title_bar_double_click() -> void:
	...


func _on_title_bar_mouse_motion(event: InputEventMouseMotion) -> void:
	...


func _on_title_bar_dragged() -> void:
	match get_window().mode:
		Window.MODE_WINDOWED:
			get_window().position += get_global_mouse_position() as Vector2i - _title_bar_dragging_start
// highlight-start
		Window.MODE_MAXIMIZED:
			_title_bar_dragging_adjustment = get_global_mouse_position().x / get_window().size.x
			get_window().mode = Window.MODE_WINDOWED


func _on_resized() -> void:
	if _title_bar_dragging_adjustment != 0:
		get_window().position += (get_global_mouse_position() as Vector2i)
		get_window().position.x -= get_window().size.x * _title_bar_dragging_adjustment
		_title_bar_dragging_start = get_global_mouse_position()
		_title_bar_dragging_adjustment = 0
// highlight-end
```

### Resize Window
Redimensionar pode ser facilmente implementado se utilizarmos o node `MarginContainer` que nos permite adicionar bordas √†s laterais, estas ser√£o nossas bordas que devem reagir ao mouse.  

Nodes do tipo `Control` possuem l√≥gica para lidar com inputs do mouse, eles podem consumir ou passar ao node de cima as input do mouse.  

Isso quer dizer que qualquer input do mouse na nossa janela (que n√£o tiver sido consumida) chegar√° ao nosso`MarginContainer`. Isto n√£o √© o que queremos, para n√≥s s√≥ √© interessante que chegue inputs interagindo com a borda do nosso container.  

Podemos resolver isto parando o consumo de inputs no container logo abaixo do `MarginContainer`:  

![Margin](margin.png)  

Agora temos certeza que intera√ß√µes vindo do signal `gui_input` s√£o intera√ß√µes diretas com o `MarginContainer`.  

```gdscript
// highlight-start
enum Margin {
	NONE,
	TOP,
	RIGHT,
	BOTTOM,
	LEFT,
	TOP_RIGHT,
	TOP_LEFT,
	BOTTOM_RIGHT,
	BOTTOM_LEFT,
}

var _margin_dragging: bool = false

var _margin_dragging_edge_start: Vector2i

var _margin_selected: Margin
// highlight-end

var _title_bar_dragging: bool = false

var _title_bar_dragging_start: Vector2i

var _title_bar_dragging_adjustment: float = 0

// highlight-start
func _get_current_margin() -> Margin:
	var margin: Margin = Margin.NONE
	
	if get_global_mouse_position().x < get_theme_constant("margin_left"):
		margin = Margin.LEFT
	elif get_global_mouse_position().x > size.x - get_theme_constant("margin_right"):
		margin = Margin.RIGHT
	
	if get_global_mouse_position().y < get_theme_constant("margin_top"):
		match margin:
			Margin.LEFT:
				return Margin.TOP_LEFT
			Margin.NONE:
				return Margin.TOP
			Margin.RIGHT:
				return Margin.TOP_RIGHT
	elif get_global_mouse_position().y > size.y - get_theme_constant("margin_bottom"):
		match margin:
			Margin.LEFT:
				return Margin.BOTTOM_LEFT
			Margin.NONE:
				return Margin.BOTTOM
			Margin.RIGHT:
				return Margin.BOTTOM_RIGHT
	
	return margin


func _on_gui_input(event: InputEvent) -> void:
	if event is InputEventMouseButton:
		_on_mouse_button(event)
	elif event is InputEventMouseMotion:
		_on_mouse_motion(event)


func _on_mouse_button(event: InputEventMouseButton) -> void:
	if event.button_index == MOUSE_BUTTON_LEFT and event.pressed:
		_margin_dragging = true
		_margin_selected = _get_current_margin()
		_margin_dragging_edge_start = get_window().position + get_window().size
	elif event.button_index == MOUSE_BUTTON_LEFT and not event.pressed:
		_margin_dragging = false


func _on_mouse_motion(_event: InputEventMouseMotion) -> void:
	if _margin_dragging:
		_on_dragged()
	else:
		_on_hover()


func _on_dragged() -> void:
	if get_window().mode != Window.MODE_WINDOWED:
		return
	
	var mouse_position: Vector2i = get_global_mouse_position()
	
	match _margin_selected:
		Margin.TOP:
			get_window().position.y += mouse_position.y # TODO: Fixing moving window
			get_window().size.y = _margin_dragging_edge_start.y - get_window().position.y
		Margin.RIGHT:
			get_window().size.x = mouse_position.x
		Margin.BOTTOM:
			get_window().size.y = mouse_position.y
		Margin.LEFT:
			get_window().position.x += mouse_position.x # TODO: Fixing moving window
			get_window().size.x = _margin_dragging_edge_start.x - get_window().position.x
		Margin.TOP_RIGHT:
			get_window().position.y += mouse_position.y # Top
			get_window().size = Vector2i(
				mouse_position.x, # Right
				_margin_dragging_edge_start.y - get_window().position.y, # Top
			)
		Margin.TOP_LEFT:
			get_window().position = Vector2i(
				get_window().position.x + mouse_position.x, # Left,
				get_window().position.y + mouse_position.y, # Top
			)

			get_window().size = Vector2i(
				_margin_dragging_edge_start.x - get_window().position.x, # Left
				_margin_dragging_edge_start.y - get_window().position.y, # Top
			)
		Margin.BOTTOM_RIGHT:
			get_window().size = Vector2i(
				mouse_position.x, # Right
				mouse_position.y, # Bottom
			)
		Margin.BOTTOM_LEFT:
			get_window().position.x += mouse_position.x # Left
			get_window().size = Vector2i(
				_margin_dragging_edge_start.x - get_window().position.x, # Left
				mouse_position.y, # Bottom
			)


func _on_hover() -> void:
	match _get_current_margin():
		Margin.TOP:
			mouse_default_cursor_shape = Control.CURSOR_VSIZE
		Margin.RIGHT:
			mouse_default_cursor_shape = Control.CURSOR_HSIZE
		Margin.BOTTOM:
			mouse_default_cursor_shape = Control.CURSOR_VSIZE
		Margin.LEFT:
			mouse_default_cursor_shape = Control.CURSOR_HSIZE
		Margin.TOP_RIGHT:
			mouse_default_cursor_shape = Control.CURSOR_BDIAGSIZE
		Margin.TOP_LEFT:
			mouse_default_cursor_shape = Control.CURSOR_FDIAGSIZE
		Margin.BOTTOM_RIGHT:
			mouse_default_cursor_shape = Control.CURSOR_FDIAGSIZE
		Margin.BOTTOM_LEFT:
			mouse_default_cursor_shape = Control.CURSOR_BDIAGSIZE
// highlight-end


func _on_minimize_pressed() -> void:
	...


func _on_maximize_pressed() -> void:
	...


func _on_close_pressed() -> void:
	...


func _on_title_bar_gui_input(event: InputEvent) -> void:
	...


func _on_title_bar_mouse_button(event: InputEventMouseButton) -> void:
	...


func _on_title_bar_double_click() -> void:
	...


func _on_title_bar_mouse_motion(_event: InputEventMouseMotion) -> void:
	...


func _on_title_bar_dragged() -> void:
	...


func _on_resized() -> void:
	...
```

Dentro das fun√ß√µes novas, muitas possuem a mesma l√≥gica utilizada para arrastar janela. Por√©m duas possuem l√≥gica nova: `_get_current_margin()` e `_on_dragged()`  

**A primeira** √© respons√°vel por identificar a borda a qual o mouse se encontra (varias valida√ß√µes para identificar a posi√ß√£o do mouse em rela√ß√£o as bordas).  

**A segunda** √© a l√≥gica de redimensionar, para resolver ela √© recomendado primeiro resolver a l√≥gica para cima, direita, baixo e esquerda (as diagonais s√£o combina√ß√µes das l√≥gicas das outras).  

:::note
Redimensionar uma janela inclui redimensionar os items dentro dela, isso pode ser um tanto quanto custoso de se fazer todas as frames.  

Eu penso em testar redimensionar de tempos em tempos e apenas se tiver algum redimensionamento pendente ü§î.  
:::

## 5 - Drag and Drop (DND)
Podemos dividir em dois tipos:
- Drag from Godot
- Drag from Operating System

Entenda que n√£o √© poss√≠vel simplesmente arrastar um item de uma aplica√ß√£o para outra e esperar que a receptora entenda aquele tipo de dado.  

Por exemplo, imagine que n√≥s puxemos a aba do terminal do VSCode para o Godot.  

![VSCode terminal](vscode_terminal.png)  

Embora VSCode nos permita arrastar est√° aba e reposiciona-la dentro do pr√≥prio VSCode, o Godot n√£o entende o que √© est√° aba (definitivamente n√£o √© um Node ou Control).  

Para resolver este problema, o sistema operacional age como intermedi√°rios entre as aplica√ß√µes, for√ßando a aplica√ß√£o a formatar de uma maneira esperada pelo OS antes de transferir entre aplica√ß√µes

:::note
Isto quer dizer que cada sistema operacional possue seu formato de transfer√™ncia (normalmente as bibliotecas abstraem isto).  
:::

Por outro lado, quando toda a opera√ß√£o de DND √© dentro do Godot, n√£o precisamos nos preocupar com formatar da maneira que o sistema operacional deseja e podemos passar os dados em um formato conhecido pelo Godot.  

### Drag from Godot
![DND Godot](dnd_godot.svg)  

No momento que voc√™ come√ßa a arrastar qualquer [Control](https://docs.godotengine.org/en/stable/classes/class_control.html), Godot ir√° chamar o m√©todo [`_get_drag_data()`](https://docs.godotengine.org/en/stable/classes/class_control.html#class-control-private-method-get-drag-data) daquele Control.  

Exemplo:  

```gdscript
extends TextureRect


func _get_drag_data(at_position: Vector2) -> Variant:
	return texture
```

- Se o m√©todo retornar `null`, Godot entender√° que n√£o existe conte√∫do sendo arrastado
	- Por padr√£o este m√©todo virtual retorna `null`
- Se o m√©todo retornar qualquer outro dado, Godot entender√° que existe conte√∫do sendo arrastado

Neste momento Godot lan√ßa a notifica√ß√£o [NOTIFICATION_DRAG_BEGIN](https://docs.godotengine.org/en/stable/classes/class_node.html#class-node-constant-notification-drag-begin) para todos os [Nodes](https://docs.godotengine.org/en/stable/classes/class_node.html).  

:::info
Este tipo de notifica√ß√£o √© muito utilizada em GUI's pois nos permite destacar uma √°rea onde o conte√∫do pode ser solto.  

Por exemplo, Godot detecta que voc√™ est√° arrastando algo v√°lido para aquele campo e cria uma borda azul para deixar claro que √© poss√≠vel soltar o conte√∫do l√°.  

![Godot blue border](godot_blue_border.png)  
:::

Agora que estamos no estado "dragging", sempre que passarmos o mouse em cima de um Control, Godot ir√° chamar o m√©todo [`_can_drop_data()`](https://docs.godotengine.org/en/stable/classes/class_control.html#class-control-private-method-can-drop-data) para saber se √© poss√≠vel soltar conte√∫do nele.  

Exemplo:  

```gdscript
extends Button


func _can_drop_data(at_position: Vector2, data: Variant) -> bool:
	return data is Texture
```

- Se o m√©todo retornar `false`, Godot entender√° que n√£o suporta o conte√∫do sendo arrastado
	- Por padr√£o este m√©todo virtual retorna `false`
- Se o m√©todo retornar `true`, Godot entender√° que suporta o conte√∫do sendo arrastado

:::info
√â normal ver o mouse mudar de apar√™ncia para destacar que o conte√∫do pode ser largado naquele local.  
:::

No momento que soltarmos o conte√∫do, Godot ir√° chamar o m√©todo [`_drop_data()`](https://docs.godotengine.org/en/stable/classes/class_control.html#class-control-private-method-drop-data) **apenas** se passou na valida√ß√£o do m√©todo `_can_drop_data()`.  

Exemplo:  

```gdscript
extends Button


func _can_drop_data(at_position: Vector2, data: Variant) -> bool:
	return data is Texture


func _drop_data(at_position: Vector2, data: Variant) -> void:
	icon = data
```

Ap√≥s soltar o click, independente se tiver sido de algo v√°lido, Godot ir√° emitir a notifica√ß√£o [NOTIFICATION_DRAG_END](https://docs.godotengine.org/en/stable/classes/class_node.html#class-node-constant-notification-drag-end) para todos os Nodes.  

:::info
√â poss√≠vel conseguir informa√ß√µes ao vivo do estado do dragging na [Viewport](https://docs.godotengine.org/en/stable/classes/class_viewport.html).  

- [`gui_get_drag_data()`](https://docs.godotengine.org/en/stable/classes/class_viewport.html#class-viewport-method-gui-get-drag-data)
	- O dado sendo arrastado
- [`gui_is_drag_successful()`](https://docs.godotengine.org/en/stable/classes/class_viewport.html#class-viewport-method-gui-is-drag-successful)
	- O resultado do √∫ltimo drag
- [`gui_is_dragging()`](https://docs.godotengine.org/en/stable/classes/class_viewport.html#class-viewport-method-gui-is-dragging)
	- O estado atual do drag
:::

### Drag from OS
![DND OS](dnd_os.svg)  

No momento Godot apenas suporta **drop** do **file manager**, ao fazer isto sua janela ir√° receber os para os arquivos passados.  

:::info
Esta a√ß√£o ocorre entre sistema operacional e as janelas da nossa aplica√ß√£o, mas √© importante entender que est√° janela **n√£o** pode ser [embedded](https://docs.godotengine.org/en/stable/classes/class_viewport.html#class-viewport-property-gui-embed-subwindows).  

Isso √© necess√°rio pois "embed window" √© apenas uma simula√ß√£o de janela dentro da nossa aplica√ß√£o, logo n√£o √© vista como janela pelo OS.  
:::

O node [Window](https://docs.godotengine.org/en/stable/classes/class_window.html) possui o sinal [files_dropped](https://docs.godotengine.org/en/stable/classes/class_window.html#class-window-signal-files-dropped) para avisar quando um ou mais arquivos s√£o largados na janela.  

Sabendo isto podemos conectar uma fun√ß√£o a este sinal da janela principal:  

```gdscript
func _ready() -> void:
	get_window().files_dropped.connect(_on_files_dropped)


func _on_files_dropped(files: PackedStringArray) -> void:
	pass
```

:::note
Se estivessemos falando de uma subwindow, poderiamos utilizar a pr√≥pria interface do Godot para conectar.  
:::

Um detalhe a se notar √© que neste caso n√£o recebemos a posi√ß√£o onde os arquivos foram largados, ent√£o precisariamos calcular manualmente se est√° dentro da √°rea esperada.  

```gdscript
func _ready() -> void:
	get_window().files_dropped.connect(_on_files_dropped)


func _on_files_dropped(files: PackedStringArray) -> void:
	if Rect2(global_position, size).has_point(get_global_mouse_position()):
		pass
```

Este c√≥digo pode ser adicionado a qualquer Control para que ele trate drops nele.  

## References
- https://github.com/thiagola92/learning-godot-window
- https://www.youtube.com/watch?v=cJ5Rkk5fnGg
- https://learn.microsoft.com/en-us/windows/apps/design/input/drag-and-drop
- https://www.freedesktop.org/wiki/Specifications/XDND/